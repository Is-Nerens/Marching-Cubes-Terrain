#version 460 core

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

// Uniforms
uniform float densityThreshold;
uniform int width;
uniform int height;
uniform int offsetX;
uniform int offsetY;
uniform int offsetZ;

// layout(binding = 0) readonly buffer VolumeDataBuffer {
//     float VolumeData[];
// };
layout(binding = 1) writeonly buffer VertexBuffer {
    float vertices[];
};
layout(binding = 2) readonly buffer TriTableBuffer {
    int TriTable[];
};


// cornerIndexAFromEdge array
const int cornerIndexAFromEdge[12] = int[](0, 1, 2, 3, 4, 5, 6, 7, 0, 1, 2, 3);

// cornerIndexBFromEdge array
const int cornerIndexBFromEdge[12] = int[](1, 2, 3, 0, 5, 6, 7, 4, 4, 5, 6, 7);


struct Corner {
    vec3 position;
    float density;
};

vec3 VertexInterp(Corner c1, Corner c2)
{
    float t = (densityThreshold- c1.density) / (c2.density - c1.density);
    vec3 v;
    v.x = c1.position.x + t * (c2.position.x - c1.position.x);
    v.y = c1.position.y + t * (c2.position.y - c1.position.y);
    v.z = c1.position.z + t * (c2.position.z - c1.position.z);
    return v;
}

vec3 CalculateNormal(vec3 v1, vec3 v2, vec3 v3)
{
    vec3 vect1 = v2 - v1;
    vec3 vect2 = v3 - v1;
    
    // Handle degenerate case, return a default normal or NaN
    if (length(vect1) < 0.00001 || length(vect2) < 0.00001) {
        return vec3(0.0, 0.0, 0.0); // Or return NaN or some default normal
    }
    
    vec3 unnormalized = cross(vect1, vect2);
    return normalize(unnormalized);
}














// PERLIN NOISE ON THE GPU
vec3 RandomGradient3D(int ix, int iy, int iz) 
{
    const uint w = 32u;
    const uint s = w / 2;
    
    uint a = uint(ix), b = uint(iy), c = uint(iz);
    a *= 3284157443U;
    b ^= a << s | a >> (w - s);
    b *= 1911520717U;
    a ^= b << s | b >> (w - s);
    a *= 2048419325U;
    float randomX = float(a) * (3.14159265 / float(0xFFFFFFFFU)); // in [0, 2*Pi]

    // Generate a random angle for the z-axis
    c *= 3329032859U;
    c ^= a << s | a >> (w - s);
    c *= 1431691223U;
    b ^= c << s | c >> (w - s);
    b *= 1812433253U;
    float randomZ = float(b) * (3.14159265 / float(0xFFFFFFFFU)); // in [0, 2*Pi]

    // Create the vector from the angles
    vec3 v;
    v.x = sin(randomX) * cos(randomZ);
    v.y = cos(randomX) * cos(randomZ);
    v.z = sin(randomZ);
    return v;
}

vec2 RandomGradient2D(int ix, int iy)
{
    const uint w = 32u;
    const uint s = w / 2;
    
    uint a = uint(ix), b = uint(iy);
    a *= 3284157443U;
    b ^= a << s | a >> (w - s);
    b *= 1911520717U;
    a ^= b << s | b >> (w - s);
    a *= 2048419325U;
    float random = float(a) * (3.14159265 / float(0xFFFFFFFFU)); // in [0, 2*Pi]

    // Create the vector from the angles
    vec2 v;
    v.x = sin(random);
    v.y = cos(random);
    return v;
}

float DotGridGradient3D(int ix, int iy, int iz, float x, float y, float z)
{
    vec3 gradient = RandomGradient3D(ix, iy, iz);
    float dx = x - float(ix);
    float dy = y - float(iy);
    float dz = z - float(iz);
    return dot(gradient, vec3(dx, dy, dz));
}

float DotGridGradient2D(int ix, int iy, float x, float y)
{
    vec2 gradient = RandomGradient2D(ix, iy);
    float dx = x - float(ix);
    float dy = y - float(iy);
    return dot(gradient, vec2(dx, dy));
}

float Interpolate(float a0, float a1, float w)
{
    return (a1 - a0) * (3.0f - w * 2.0f) * w * w + a0;
}

float Perlin3D(float x, float y, float z)
{
    // Grid cell corner coordinates
    int x0 = int(floor(x));
    int y0 = int(floor(y));
    int z0 = int(floor(z));
    int x1 = x0 + 1;
    int y1 = y0 + 1;
    int z1 = z0 + 1;

    // Interpolation weights
    float sx = x - float(x0);
    float sy = y - float(y0);
    float sz = z - float(z0);

    // Bottom corners
    float n0 = DotGridGradient3D(x0, y0, z0, x, y, z);
    float n1 = DotGridGradient3D(x1, y0, z0, x, y, z);
    float n2 = DotGridGradient3D(x0, y1, z0, x, y, z);
    float n3 = DotGridGradient3D(x1, y1, z0, x, y, z);

    // Top corners
    float n4 = DotGridGradient3D(x0, y0, z1, x, y, z);
    float n5 = DotGridGradient3D(x1, y0, z1, x, y, z);
    float n6 = DotGridGradient3D(x0, y1, z1, x, y, z);
    float n7 = DotGridGradient3D(x1, y1, z1, x, y, z);

    // Trilinear interpolation
    float ix0 = Interpolate(n0, n1, sx);
    float ix1 = Interpolate(n2, n3, sx);
    float ix2 = Interpolate(n4, n5, sx);
    float ix3 = Interpolate(n6, n7, sx);

    float iy0 = Interpolate(ix0, ix1, sy);
    float iy1 = Interpolate(ix2, ix3, sy);

    float interpolatedValue = Interpolate(iy0, iy1, sz);
    return (interpolatedValue + 1.0) / 2.0;
}

float Perlin2D(float x, float y)
{
    // Grid corner coordinates
    int x0 = int(floor(x));
    int y0 = int(floor(y));
    int x1 = x0 + 1;
    int y1 = y0 + 1;

    // Interpolation weights
    float sx = x - float(x0);
    float sy = y - float(y0);

    // Corners
    float n0 = DotGridGradient2D(x0, y0, x, y);
    float n1 = DotGridGradient2D(x1, y0, x, y);
    float n2 = DotGridGradient2D(x0, y1, x, y);
    float n3 = DotGridGradient2D(x1, y1, x, y);

    // Interpolation
    float ix0 = Interpolate(n0, n1, sx);
    float ix1 = Interpolate(n2, n3, sx);

    float interpolatedValue = Interpolate(ix0, ix1, sy);
    return interpolatedValue;
}

float GetDensity(float x, float y, float z)
{
    float yOriginal = y + offsetY;
    float surfaceHeight = 100 + (Perlin2D((x + offsetX) * 0.015, (z + offsetZ) * 0.015) * 20 + Perlin2D((x + offsetX) * 0.03, (z + offsetZ) * 0.03) * 10);

    if (yOriginal <  surfaceHeight - 1)
    {
        x = (x + offsetX) * 0.07;
        y = (y + offsetY) * 0.07;
        z = (z + offsetZ) * 0.07;
        return Perlin3D(x, y, z) + Perlin3D(x*2, y*2, z*2) * 0.3 + Perlin3D(x*4, y*4, z*4) * 0.12 + Perlin3D(x*8, y*8, z*8) * 0.05;
    }
    // smooth out jagged surface
    else if (yOriginal <= surfaceHeight && yOriginal > surfaceHeight -1.0)
    {
        x = (x + offsetX) * 0.07;
        y = (y + offsetY) * 0.07;
        z = (z + offsetZ) * 0.07;
        float density = Perlin3D(x, y, z) + Perlin3D(x*2, y*2, z*2) * 0.3 + Perlin3D(x*4, y*4, z*4) * 0.12 + Perlin3D(x*8, y*8, z*8) * 0.05;
        float transition = surfaceHeight - yOriginal;
        return density * transition;
    }
    else
    {
        return 0.0;
    }
}
// float GetDensity(float xF, float yF, float zF)
// {
//     int x = int(floor(xF + 0.5));
//     int y = int(floor(yF + 0.5));
//     int z = int(floor(zF + 0.5));
//     int index = z + (width+1) * (x + (height+1) * y);
//     return VolumeData[index];
// }



















int TriTableGet(int cubeIndex, int i)
{
    int index = cubeIndex * 16 + i;
    return TriTable[index];
}

void AddFace(vec3 v1, vec3 v2, vec3 v3, vec3 normal, int index)
{
    // vertex 1
    vertices[index] = v1.x;
    vertices[index + 1] = v1.y;
    vertices[index + 2] = v1.z;

    // vertex 2
    vertices[index + 3] = v2.x;
    vertices[index + 4] = v2.y;
    vertices[index + 5] = v2.z;

    // vertex 3
    vertices[index + 6] = v3.x;
    vertices[index + 7] = v3.y;
    vertices[index + 8] = v3.z;

    // normal
    vertices[index + 9] = normal.x;
    vertices[index + 10] = normal.y;
    vertices[index + 11] = normal.z;
}


void main()
{
    int threadID = int(gl_GlobalInvocationID.x) +
               int(gl_GlobalInvocationID.y) * int(gl_NumWorkGroups.x) * int(gl_WorkGroupSize.x) +
               int(gl_GlobalInvocationID.z) * int(gl_NumWorkGroups.x) * int(gl_NumWorkGroups.y) * int(gl_WorkGroupSize.x) * int(gl_WorkGroupSize.y);


    // create 8 corners
    Corner corners[8];
    corners[0].position = vec3(gl_GlobalInvocationID.x - 0.5, gl_GlobalInvocationID.y - 0.5, gl_GlobalInvocationID.z + 0.5);
    corners[1].position = vec3(gl_GlobalInvocationID.x + 0.5, gl_GlobalInvocationID.y - 0.5, gl_GlobalInvocationID.z + 0.5);
    corners[2].position = vec3(gl_GlobalInvocationID.x + 0.5, gl_GlobalInvocationID.y - 0.5, gl_GlobalInvocationID.z - 0.5);
    corners[3].position = vec3(gl_GlobalInvocationID.x - 0.5, gl_GlobalInvocationID.y - 0.5, gl_GlobalInvocationID.z - 0.5);
    corners[4].position = vec3(gl_GlobalInvocationID.x - 0.5, gl_GlobalInvocationID.y + 0.5, gl_GlobalInvocationID.z + 0.5);
    corners[5].position = vec3(gl_GlobalInvocationID.x + 0.5, gl_GlobalInvocationID.y + 0.5, gl_GlobalInvocationID.z + 0.5);
    corners[6].position = vec3(gl_GlobalInvocationID.x + 0.5, gl_GlobalInvocationID.y + 0.5, gl_GlobalInvocationID.z - 0.5);
    corners[7].position = vec3(gl_GlobalInvocationID.x - 0.5, gl_GlobalInvocationID.y + 0.5, gl_GlobalInvocationID.z - 0.5);

    // calculate the cube index
    int cubeIndex = 0;
    for (int i = 0; i < 8; ++i) {
        corners[i].density = GetDensity(corners[i].position.x, corners[i].position.y, corners[i].position.z);
        if (corners[i].density > densityThreshold) cubeIndex |= (1 << i);
    }

    // Fill cubeVertices and cubeNormals arrays
    int i = 0;
    while(TriTableGet(cubeIndex, i) != -1)
    {    
        int a0 = cornerIndexAFromEdge[TriTableGet(cubeIndex, i)];
        int b0 = cornerIndexBFromEdge[TriTableGet(cubeIndex, i)];
        int a1 = cornerIndexAFromEdge[TriTableGet(cubeIndex, i+1)];
        int b1 = cornerIndexBFromEdge[TriTableGet(cubeIndex, i+1)];
        int a2 = cornerIndexAFromEdge[TriTableGet(cubeIndex, i+2)];
        int b2 = cornerIndexBFromEdge[TriTableGet(cubeIndex, i+2)];
        
        vec3 v1 = VertexInterp(corners[a0], corners[b0]);
        vec3 v2 = VertexInterp(corners[a1], corners[b1]);
        vec3 v3 = VertexInterp(corners[a2], corners[b2]);
        vec3 normal = CalculateNormal(v1, v2, v3);

        AddFace(v1, v2, v3, normal, threadID * 48 + i * 4);
        i += 3;
    }
}








